group 'com.bluespurs.starterkit'
version '1.0.0'

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.3.2.RELEASE'
        classpath 'org.liquibase:liquibase-gradle-plugin:1.2.1'
        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:1.2'
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'spring-boot'
apply plugin: 'liquibase'
apply plugin: 'war'
apply plugin: 'jacoco'
apply plugin: 'org.sonarqube'

// Load the properties file.
Properties props = new Properties();
props.load(new FileReader(projectDir.getAbsolutePath() + '/src/main/resources/application.properties'))

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

sonarqube {
    properties {
        property "sonar.projectName", "Spring Starter Kit Project"
        property "sonar.sourceEncoding", "UTF-8"
    }
}

jacoco {
    toolVersion = '0.7.5.201505241946'
}

war {
    baseName = 'MySpringProject'
}

configurations {
    providedRuntime
}

ext {
    liquibaseHibernateVersion = '3.5'
    gsonVersion = '2.5'
    guavaVersion = '19.0'

    hibernateEntityManagerVersion = '4.3.11.Final'
    hibernateValidatorVersion = '5.2.2.Final'

    hamcrestVersion = '1.3'
    junitVersion = '4.12'
    mockitoVersion = '1.10.19'
    h2databaseVersion = '1.4.191'
    restAssuredVersion = '2.8.0'
    jsonPathAssertVersion = '0.8.1'
}

dependencies {
    // Spring Boot
    compile 'org.springframework.boot:spring-boot-devtools'
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-hateoas'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-starter-security'
    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'

    // JPA Provider
    compile "org.hibernate:hibernate-entitymanager:$hibernateEntityManagerVersion",
            "org.hibernate:hibernate-validator:$hibernateValidatorVersion"

    // Liquibase integration
    compile("org.liquibase.ext:liquibase-hibernate4:$liquibaseHibernateVersion") {
        exclude group: 'org.hibernate.javax.persistence'
    }

    // Google libraries
    compile "com.google.code.gson:gson:$gsonVersion",
            "com.google.guava:guava:$guavaVersion"

    // Database driver
    runtime "com.h2database:h2:$h2databaseVersion"

    // Unit & Integration Testing
    testCompile "org.hamcrest:hamcrest-all:$hamcrestVersion",
        "junit:junit:$junitVersion",
        "org.mockito:mockito-core:$mockitoVersion",
        "com.jayway.restassured:rest-assured:$restAssuredVersion",
        "com.jayway.jsonpath:json-path-assert:$jsonPathAssertVersion"

    testCompile 'org.springframework.boot:spring-boot-starter-test'
}

diffChangeLog.dependsOn classes
bootRepackage.dependsOn test

bootRepackage {
    excludeDevtools = true
}

liquibase {
    activities {
        main {
            changeLogFile 'src/main/resources/db-changelog.xml'
            classpath sourceSets.main.runtimeClasspath.asPath
            url props['db.url']
            referenceUrl String.format('hibernate:spring:%s?dialect=%s', props['base-package'], props['db.dialect'])
            username props['db.username']
            password props['db.password']
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = 2.11
}